import org.ajoberstar.grgit.Grgit

plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id "org.ajoberstar.grgit" version "4.1.1"
    id("io.freefair.lombok") version "8.3"

}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
//    maven { url "https://oss.sonatype.org/service/local/repositories/snapshots/content/" }
}

def versionFile = file('VERSION')
def getScmVersion = {
    def git = Grgit.open(dir: file('.')) as Grgit
    if (!['master', 'main'].contains(git.branch.current().name)) {
        version = versionFile.text.trim() + '-SNAPSHOT'
    } else {
        version = versionFile.text.trim()
    }
}

version = project.findProperty('version') != 'unspecified' ? project.version : getScmVersion() ?: '0.0.1-SNAPSHOT'
group = 'net.gradleutil'

file('VERSION').text.with { versionText ->
    if (!versionText == version) {
        logger.lifecycle("Updated version VERSION file from ${versionText} to ${version}")
        file('VERSION').text = version
    }
}

dependencies {
    implementation 'org.codehaus.groovy:groovy:3.0.11'
    implementation 'org.codehaus.groovy:groovy-swing:3.0.11'
    implementation 'com.typesafe:config:1.4.2'
    implementation 'org.json:json:20240303'
//    implementation('io.vertx:vertx-json-schema:4.5.3')
    implementation(group: 'com.networknt', name: 'json-schema-validator', version: '1.3.1')
    implementation(group: 'com.github.victools', name: 'jsonschema-generator', version: '4.33.1')
    implementation('com.github.victools:jsonschema-generator:4.33.1')

//    TODO: spec 12
//    implementation("com.github.erosb:json-sKema:0.8.0")
//    implementation 'com.github.everit-org.json-schema:org.everit.json.schema:1.14.2'
    implementation('com.squareup.moshi:moshi:1.15.1')

//    implementation 'org.eclipse.emfatic:org.eclipse.emfatic.core:0.8.0-SNAPSHOT'
//    implementation group: 'org.eclipse.emf', name: 'org.eclipse.emf.ecore', version: '2.23.0'

    testImplementation('org.spockframework:spock-core:2.1-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }

    testImplementation('org.junit.jupiter:junit-jupiter-api')

}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

task jarTests(type: Jar, dependsOn: testClasses) {
    archiveClassifier.set 'tests'
    from sourceSets.test.output
}

jar {
    enabled(false)
    archiveClassifier.set('core')
    dependsOn(jarTests)
    dependsOn(shadowJar)
}

shadowJar {
    zip64 = true

    mergeServiceFiles()
    mergeGroovyExtensionModules()
    minimize {
        exclude(dependency('com.squareup.moshi:.*:.*'))
        exclude(dependency('com.typesafe:.*:.*'))
        exclude(dependency('com.github.victools:.*:.*'))
    }
    archiveClassifier.set('')
    relocate 'org.json', 'net.gradleutil.conf.json'
    relocate 'org.apache', 'net.gradleutil.conf'
    relocate 'org.everit', 'net.gradleutil.conf'
    relocate 'com.typesafe', 'net.gradleutil.conf'
    relocate 'com.damnhandy', 'net.gradleutil.conf'
    relocate 'com.squareup', 'net.gradleutil.conf'
    relocate 'com.github.victools','net.gradleutil.conf'
    exclude 'META-INF/resources/'
    exclude 'META-INF/versions/'
    exclude 'META-INF/native/'
    exclude 'META-INF/maven/'
    exclude 'META-INF/*.txt'
    exclude 'LICENSE.txt'
    dependencies {
        exclude(dependency('org.codehaus.groovy:.*'))
        exclude(dependency('org.spockframework:.*'))
        exclude(dependency('org.junit.jupiter:.*'))
        exclude(dependency('org.junit.platform:.*'))
        exclude(dependency('org.junit:.*'))
        exclude(dependency('junit:.*'))
        exclude(dependency('info.picocli:.*'))
        exclude(dependency('org.opentest4j:.*'))
        exclude(dependency('org.testng:.*'))
        exclude(dependency('org.apache.ant:.*'))
        exclude(dependency('joda-time:.*'))
//        exclude(dependency('org.jetbrains.kotlin:.*'))
        exclude(dependency('org.jetbrains:.*'))
        //exclude(dependency { println it.moduleGroup })
    }
    dependencies {
        //  exclude(dependency { exclude_modules.contains(it.name) })
    }

    exclude('org/everit', 'META-INF/maven/org.everit.json/')
    doLast {
        def jarFile = outputs.files.first() as File
        def formatStr = "%,10.2f"
        logger.lifecycle("Shadow jar:" + jarFile + "${String.format(formatStr, jarFile.length() / 1024)} Mb")
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifact sourcesJar
            artifact javadocJar
            artifact jarTests
        }
    }
}

task artifactList {
    group = "Help"
    description = "Displays the artifacts associated with each configuration of " + project
    doFirst {
        configurations.findAll().each { config ->
            println "${config}:"
            config.allArtifacts.getFiles().each { file -> println " " + file }
            config.dependencies.each { dep -> println " dep:" + dep.toString() }
            if(config.canBeResolved){
                config.files.each{println it.name + ' ' + it}
            }
            println ' '
        }
    }
}

tasks.register("depsize") {
    description = 'Prints dependencies for "default" configuration'
    doLast() {
        listConfigurationDependencies(configurations.default)
    }
}

tasks.register("depsize-all") {
    description = 'Prints dependencies for all available configurations'
    doLast() {
        configurations
                .findAll { it.isCanBeResolved() }
                .each { listConfigurationDependencies(it) }
    }
}

def listConfigurationDependencies(Configuration configuration) {
    def formatStr = "%,10.2f"

    def size = configuration.collect { it.length() / (1024 * 1024) }.sum()

    def out = new StringBuffer()
    out << "\nConfiguration name: \"${configuration.name}\"\n"
    if (size) {
        out << 'Total dependencies size:'.padRight(65)
        out << "${String.format(formatStr, size)} Mb\n\n"

        configuration.sort { -it.length() }
                .each {
                    out << "${it.name}".padRight(65)
                    out << "${String.format(formatStr, (it.length() / 1024))} kb\n"
                }
    } else {
        out << 'No dependencies found'
    }
    println(out)
}
